plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'com.tcoded'
version = '2.0.0'

repositories {
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    mavenCentral()
//    maven {
//        name = "spigotmc-repo"
//        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
//    }

    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://maven.evokegames.gg/snapshots' }
    maven { url 'https://repo.codemc.io/repository/maven-releases/' }

    maven { url 'https://jitpack.io' }
}

dependencies {
//    compileOnly 'org.projectlombok:lombok:1.18.28'
//    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'

    implementation 'me.tofaa.entitylib:spigot:2.4.11-SNAPSHOT'
    implementation "com.github.technicallycoded:FoliaLib:0.4.3"
    compileOnly "net.kyori:adventure-text-minimessage:4.17.0"
    compileOnly 'com.github.retrooper:packetevents-spigot:2.6.0'
//    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.1'
}

def targetJavaVersion = 21

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName.set("HologramLib-${version}.jar")

    relocate 'com.tcoded.folialib', 'com.tcoded.hologramlib.lib.folialib'
    relocate 'me.tofaa.entitylib', 'com.tcoded.hologramlib.lib.entitylib'

    exclude "org/jetbrains/**"
    exclude "org/intellij/**"
}

tasks.assemble {
    dependsOn shadowJar
}